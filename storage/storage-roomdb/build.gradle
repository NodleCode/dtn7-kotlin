plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.cookpad.android.plugin.license-tools'
    id 'maven-publish'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    // small hack for room db testing so that it finds the json schemas
    sourceSets {
        debug.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    publishing {
        publications {
            libraryJar(MavenPublication) {
                from components.release
                groupId = 'com.github.NodleCode.dtn7-kotlin'
                artifactId = 'storage-roomdb'
                version = gitSha()
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api group: 'org.slf4j', name:'slf4j-api', version: "$slf4_logger"

    // bpv7
    implementation project(path: ':bpv7')

    // room database
    api "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // android + kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // test only dependencies
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutine"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "$slf4_logger"
    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test.ext:junit:$extJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
}
